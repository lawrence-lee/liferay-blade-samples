sourceSets {
	testFunctional {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/testFunctional/java')
		}
		resources.srcDir file('src/testFunctional/resources')
	}
}

configurations {
	autoLogin
	testFunctionalCompile.extendsFrom testCompile
	testFunctionalRuntime.extendsFrom testRuntime
}

dependencies {
	autoLogin project(':tests:test-auto-login')

	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "3.0.0"
	compileOnly group: "javax.portlet", name: "portlet-api", version: "3.0.0"
	compileOnly group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"

	testCompile group: "biz.aQute.bnd", name: "biz.aQute.bnd", version: "3.5.0"
	testCompile group: "biz.aQute.bnd", name: "biz.aQute.remote.api", version: "3.5.0"
	testCompile group: "com.liferay.arquillian", name: "com.liferay.arquillian.arquillian-container-liferay", version: "1.0.6"
	testCompile group: "junit", name: "junit", version: "4.12"
	testCompile group: "org.jboss.arquillian.graphene", name: "graphene-webdriver", version: "2.3.2"
	testCompile group: "org.jboss.arquillian.junit", name: "arquillian-junit-container", version: "1.1.11.Final"

	testCompile (project(':tests:utils')) {
		transitive = false
	}
}

task deployAutoLogin(type: Copy) {
	from configurations.autoLogin
	into new File(liferay.liferayHome, "osgi/modules")
}

deployAutoLogin.dependsOn ":tests:test-auto-login:jar"

startTestableTomcat {
	checkTimeout = 10 * 60 * 1000

	finalizedBy {
		deployAutoLogin
	}

}

task testFunctional(type: Test) {
	testClassesDir = sourceSets.testFunctional.output.classesDir
	classpath = sourceSets.testFunctional.runtimeClasspath
	outputs.upToDateWhen { false }
	
	dependsOn startTestableTomcat

	dependsOn ":apps:npm:angular-npm-portlet:jar"
	dependsOn ":apps:npm:billboardjs-npm-portlet:jar"
	dependsOn ":apps:npm:isomorphic-npm-portlet:jar"
	dependsOn ":apps:npm:jquery-npm-portlet:jar"
	dependsOn ":apps:npm:metaljs-npm-portlet:jar"
	dependsOn ":apps:npm:react-npm-portlet:jar"
	dependsOn ":apps:npm:simple-npm-portlet:jar"
	dependsOn ":apps:npm:vuejs-npm-portlet:jar"

	doFirst {
		systemProperty 'angularNpmPortletJarFile', project(':apps:npm:angular-npm-portlet').jar.archivePath
		systemProperty 'billboardjsNpmPortletJarFile', project(':apps:npm:billboardjs-npm-portlet').jar.archivePath
		systemProperty 'isomorphicNpmPortletJarFile', project(':apps:npm:isomorphic-npm-portlet').jar.archivePath
		systemProperty 'jqueryNpmPortletJarFile', project(':apps:npm:jquery-npm-portlet').jar.archivePath
		systemProperty 'metaljsNpmPortletJarFile', project(':apps:npm:metaljs-npm-portlet').jar.archivePath
		systemProperty 'reactNpmPortletJarFile', project(':apps:npm:react-npm-portlet').jar.archivePath
		systemProperty 'simpleNpmPortletJarFile', project(':apps:npm:simple-npm-portlet').jar.archivePath
		systemProperty 'vuejsNpmPortletJarFile', project(':apps:npm:vuejs-npm-portlet').jar.archivePath
		systemProperty 'liferayHome', liferay.liferayHome
		systemProperty 'projectDir', rootProject.projectDir
	}

	systemProperty 'bladeURL', System.getProperty('bladeURL', "https://releases.liferay.com/tools/blade-cli/latest/blade.jar")
	systemProperty 'portalVersion', project.getProperty("liferay.workspace.bundle.url")

	doLast {
		delete new File("blade.jar")
		delete new File("target")
	}

	finalizedBy {
		stopTestableTomcat
	}
}

stopTestableTomcat {
	doLast {
		delete new File(liferay.liferayHome, "data")
	}
}

check.dependsOn testFunctional